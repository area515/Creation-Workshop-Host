// PROJECT CONFIG
// --------------
plugins {
  id "java"
  id "co.riiid.gradle" version "0.4.2"
  id 'org.ajoberstar.grgit' version '1.4.2'
  id 'com.google.osdetector' version '1.4.0'
}

repositories {
  mavenCentral()
}

configurations {

}

sourceSets {
  test {
    java {
      srcDir 'tests'
    }
    resources {
      srcDir 'tests'
    }
    compileClasspath += (
      fileTree(dir: 'libs', include: '**/*.jar')
      + files('srcbin')
    )
    runtimeClasspath += compileClasspath
  }
}

dependencies {

}

github {
  // owner and repo will be determined from the origin
  // owner = releaseRepoOwner
  // repo = releaseRepoName

  // Set token in gradle.properties
  token = releaseRepoToken;
  // tagName = 'd001'
  // Must compute assets in task, otherwise you can't release with dist
  // assets = ant.path {fileset(dir: '.', includes: '*.zip')} . list()
};

// PROJECT SETUP SCRIPT
// --------------------
import org.ajoberstar.grgit.*;
import org.gradle.internal.os.OperatingSystem;

ant.importBuild 'build.xml';

grgit = Grgit.open(project.file('..'));
def commitVersion = "unknown";

// TASK CONFIG
// -----------
test {
  systemProperty "java.library.path", osLibDir()
  includes = ['**/*FullTestSuite*']
}

// TASK EXECUTION DEFS
// -------------------
task repoVersion() << {
  commitVersion = grgit.describe()

  if (commitVersion == null) {
    def history = grgit.log(maxCommits: 1)
    commitVersion = history[0].id.take(5)
  }

  ant.properties['repo.version'] = commitVersion

  println("Git Commit Description: "+commitVersion)
};

task preDist(dependsOn: ['build', 'compileTestJava', 'processTestResources', 'repoVersion']) {

}

task dist(dependsOn: ['preDist', 'BuildCWH']) {

};

task setupRemote << {
  def matchingRemotes = grgit.remote.list().findAll({it.name==releaseRepoRemoteName});
  if (matchingRemotes.size < 1) {
    throw new GradleException("No configured remotes match the the remote named: ${releaseRepoRemoteName}");
  }

  def remote = matchingRemotes;
  //def releaseUrl = "https://github.com/${github.owner}/${github.repo}"
  def releaseUrl = remote.url;
  println("Releasing to: "+releaseUrl);

  def repoMatch = (releaseUrl =~ /github\.com[\/\:]([^\/]+)\/([^\/]*?)(\.git)?.?$/);

  def repoOwner = repoMatch[0][1];
  def repoName = repoMatch[0][2];

  println("Repo Owner: ${repoOwner} / Repo Name: ${repoName}");

  github.owner = repoOwner;
  github.repo = repoName;
}


task setupRelease(dependsOn: ['setupRemote']) << {
  grgit = Grgit.open(project.file('..'), new Credentials(username: github.owner, password: github.token));

  if (releaseRepoToken.equals('GITHUB_API_TOKEN')) {
    throw new GradleException("Please setup your gradle.properties for GitHub Releases")
  }
  def status = grgit.status();
  println("----------------------------------------------")
  println("Description of current commit: "+grgit.describe())
  println("UNCOMMITTED STAGED CHANGES: "+status.staged)
  println("UNCOMMITTED UNSTAGED CHANGES: "+status.unstaged)
  println("----------------------------------------------")

  def tagName = "$System.env.tagName";

  println("Dry-Run Git Push to ${releaseRepoRemoteName}:")
  grgit.push(dryRun: true, remote: releaseRepoRemoteName);
  println("----------------------------------------------")
  println("Check the above git push dry run to ensure that your push will succeed!");
  ant.input(message: "Press <return> to continue...")

  if (tagname == null) {
    ant.input(message: "Enter the tag to create for this release > ", addproperty: "tagName")
    tagName = ant.tagName
  }

  github.tagName = tagName;
  grgit.tag.add(name: tagName);
}

task releaseDist (dependsOn: ['dist']) << {
  github.assets = fileTree('.').include("*.zip") as List;
  if (github.assets.size() < 1) {
    throw new GradleException("There are no zip files to release in the project directory.")
  }
}

task fullRelease(dependsOn: ['setupRelease', 'githubRelease']) << {
  grgit.push(tags: true, remote: releaseRepoRemoteName);
}

clean.doFirst {
  // Additions to the built-in clean
  delete "srcbin", "testbin", "cwh.log"
  delete fileTree(dir: '.', include: '*.zip')
}

// PROJECT POST-SETUP SCRIPT
// -------------------------
githubRelease.dependsOn releaseDist
dist.mustRunAfter setupRelease
compileTestJava.dependsOn build
BuildCWH.mustRunAfter preDist

// UTILITY FUNCTIONS
// -----------------
String osLibDir() {
  switch (osdetector.classifier) {
    case "windows-x86_64":
      return 'os/win64'
    case "windows-x86_32":
      return 'os/win32'
    case "linux-x86_64":
      return 'os/Linux/x86_64'
    case "linux-x86_32":
      return 'os/Linux/i686'
    case "linux-arm_32":
      return 'os/Linux/armv61'
    default:
      return 'unknown'
  }
}
